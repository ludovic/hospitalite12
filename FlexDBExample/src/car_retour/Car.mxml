<?xml version="1.0" encoding="utf-8"?>
<mx:VBox xmlns:mx="http://www.adobe.com/2006/mxml" width="100%" height="100%" creationComplete="canvas1_creationCompleteHandler(event)"  verticalScrollPolicy="off">
	<mx:Script>
		<![CDATA[
			import common.helper.DataGridDataExporter;
			
			import mx.collections.ArrayCollection;
			import mx.controls.Alert;
			import mx.events.CloseEvent;
			import mx.events.FlexEvent;
			
			import phi.db.Query;
			
			import proxy.AffectationProxy;
			import proxy.CarProxy;
			
			public static var RELOAD_MODULE:String = "reloadModule";
			private var moduleList:Array = [];
			
			protected function canvas1_creationCompleteHandler(event:FlexEvent=null):void
			{
				containerSans.removeAllChildren();
				container.removeAllChildren();
				var carArray:ArrayCollection = CarProxy.CarDispo;
				var moduleSans:ModuleSansCar = new ModuleSansCar();
				
				moduleList.push({module:moduleSans,car:{id_transport:0}});
				containerSans.addChild(moduleSans);
				
				moduleSans.addEventListener(RELOAD_MODULE,provideModules); 
				var tabList:Array = [];
				for(var i:int=0;i<carArray.length;++i)
				{
					var carName:String = carArray[i].nom_transport as String
					if((searchInput.text == "")||(carName.toLowerCase().search(searchInput.text.toLowerCase()) != -1))
					{
						var module:ModuleCar = new ModuleCar();
						module.titleText = carArray[i].nom_transport as String;
						module.capacite = carArray[i].Capacite as int;
						moduleList.push({module:module,car:carArray[i]});
						container.addChild(module);
						tabList.push(module.titleText);
						module.addEventListener(RELOAD_MODULE,provideModules);
					}
				}
				 provideModules();  
			/* 	leftTabs.dataProvider(tabList); */
			}
			
			public function provideModules(evt:Object=null):void
			{
				for(var j:int=0;j<moduleList.length;j++)
				{
					moduleList[j].module.generateModuleProvider(moduleList[j].car.id_transport);
				}
			}
			
			///////////////////////// SEARCH ////////////////////////////////////////////////////
			
			private var searchTimer:Timer;
			
			public function filter():void
			{
				if(!searchTimer)
				{
					searchTimer = new Timer(500,1);
					searchTimer.addEventListener(TimerEvent.TIMER_COMPLETE,function(evt:Event):void{
						canvas1_creationCompleteHandler();
					});
				}	
				else
					searchTimer.reset();
				
				searchTimer.start();
			}
			
			private var exportDatagrid:ArrayCollection;
			protected function exportDatagridFunction(event:MouseEvent):void
			{
				exportDatagrid = new ArrayCollection();
				for(var i:int =0; i<moduleList.length;i++)
				{
					if(moduleList[i].module is ModuleCar)
					{
						exportDatagrid.addItem({nom:(moduleList[i].module as ModuleCar).titleText});
						exportDatagrid.addItem({a_nom:"Nom",b_prenom:"Prenom",c_gare:"Gare",d_personne_type:"Categorie",e_personne_role:"Role"});
						var provider:ArrayCollection = (moduleList[i].module as ModuleCar).provider;
						for(var j:int=0;j<provider.length;j++)
						{
							exportDatagrid.addItem({a_nom:provider[j].nom, b_prenom:provider[j].prenom, c_gare:provider[j].gare, d_personne_type:provider[j].personne_type, e_personne_role:provider[j].personne_role});
						}
					}
				}
				exportDatagrid.addItem({nom:" "});
				provideExportDatagrid();
			}
			
			private function provideExportDatagrid():void
			{
				Alert.show("Voulez-vous exporter la liste des cars ?","",Alert.OK|Alert.CANCEL,this, alertClickHandler);
			}
			
			private function alertClickHandler(event:CloseEvent):void
			{
				switch (event.detail){
					case Alert.OK:
						var csvText:String = DataGridDataExporter.exportCSVFromArrayCollection(exportDatagrid,false);
						var fileSave:FileReference= new FileReference();
						fileSave.save(csvText,"export_cars_retour.csv");
						break;
					case Alert.CANCEL:
						break;
				}
				
			}
		]]>
	</mx:Script>
	
	<mx:HBox width="100%" height="30">
		<mx:Canvas>
			<mx:Image source="@Embed('/assets/search.png')" />
			<mx:TextInput id="searchInput" left="20" borderStyle="none" backgroundAlpha="0" width="110" focusThickness="0" change="{this.filter()}"
						  borderSkin="@Embed('/assets/blank.png')" textAlign="left"
						  />
		</mx:Canvas>
		<mx:Button icon="@Embed(source='assets/reload.gif')" click="provideModules(event)" height="22"/>
		<mx:Spacer width="100%"/>
		<mx:Button label="Exporter Liste des cars" click="exportDatagridFunction(event)"/>
	</mx:HBox>
	<mx:HBox width="100%" height="100%" verticalScrollPolicy="off">
		<mx:VBox id="container"  width="100%" height="{this.height*0.9}">
		</mx:VBox> 
		<mx:VBox id="containerSans"  width="100%" height="100%">
		</mx:VBox>
		
	</mx:HBox>
	
</mx:VBox>
